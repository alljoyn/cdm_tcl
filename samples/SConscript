import os

Import('env')

samples_env = env.Clone()

samples_env.Append(CPPDEFINES = {
    'AJ_NUM_REPLY_CONTEXTS': '8',
    'AJ_MAX_OBJECT_LISTS': '9',
    'ANNOUNCE_BASED_DISCOVERY': '1'
})
samples_env.Append(CPPPATH = './inc')

samples_env.Append(LIBPATH = ['#dist/../../../core/ajtcl/dist/lib', '#dist/../../base_tcl/dist/lib', '#dist/lib'])
samples_env.Prepend(LIBS = ['ajtcl', 'ajtcl_services', 'ajtcl_services_config', 'ajtcl_cdm'])

source_files = [
	'common/*.c',
	'common/HWMock/*.c',
	'common/HWMock/interfaces/environment/*.c',
	'common/HWMock/interfaces/input/*.c',
	'common/HWMock/interfaces/operation/*.c',
	'common/HWMock/interfaces/userinterfacesettings/*.c',
	'common/SW/*.c',
	'common/SW/interfaces/environment/*.c',
	'common/SW/interfaces/input/*.c',
	'common/SW/interfaces/operation/*.c',
	'common/SW/interfaces/userinterfacesettings/*.c'
]

srcs = [samples_env.Glob(src) for src in source_files]
common_objs = samples_env.Object(srcs)

build_samples = [
    ('MyDeviceControllee', 'MyDeviceHWMock.rtf')	# (sample name, HW mock file name)
]

progs = []
hw_mock_files = []

for sample in build_samples:
    objs = samples_env.Object(samples_env.Glob(sample[0] + '/*.c'))
    progs.append(samples_env.Program(sample[0] + 'Sample', common_objs + objs))
    hw_mock_files += samples_env.Glob(sample[0] + '/' + sample[1])

samples_env.Install('#dist/bin', progs)
samples_env.Install('#dist/bin/HWMockFiles', hw_mock_files)
